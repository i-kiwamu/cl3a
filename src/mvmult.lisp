(in-package :cl-user)
(defpackage cl3a.mvmult
  (:use :cl :alexandria :cl3a.utilities)
  (:export :dm*v :lm*v))
(in-package :cl3a.mvmult)


(defmacro m*v-ker (val-type si ni sj nj nr nc ma vb vc)
  "Multiply matrix and vector"
  (with-gensyms (iend jend i j ima ivb vci)
    `(let* ((,iend (min ,nr (the fixnum (+ ,si ,ni))))
            (,jend (min ,nc (the fixnum (+ ,sj ,nj)))))
       (declare (type fixnum ,iend ,jend))
       (do ((,i ,si (1+ ,i)))
           ((>= ,i ,iend))
         (let ((,ima (array-row-major-index ,ma ,i ,sj))
               (,ivb (array-row-major-index ,vb ,sj))
               (,vci (coerce 0.0 ',val-type)))
           (declare (type fixnum ,ima ,ivb)
                    (type ,val-type ,vci))
           (do ((,j ,sj (1+ ,j)))
               ((>= ,j ,jend))
             (incf ,vci
                   (* (row-major-aref ,ma ,ima)
                      (row-major-aref ,vb ,ivb)))
             (incf ,ima)
             (incf ,ivb))
           (incf (aref ,vc ,i) ,vci))))))


(defmacro m*v (val-type ma vb vc)
  (with-gensyms (calc nra nca nb nj m i)
    `(flet ((,calc (si ni sj nj nr nc ma vb vc)
              (declare (optimize (speed 3) (debug 0) (safety 0))
                       (type fixnum si ni sj nj nr nc)
                       (type (simple-array ,val-type (* *)) ma)
                       (type (simple-array ,val-type (*)) vb vc))
              (m*v-ker ,val-type si ni sj nj nr nc ma vb vc)))
       (declare (inline ,calc))
       (let* ((,nra (array-dimension ,ma 0))
              (,nca (array-dimension ,ma 1))
              (,nb (length ,vb))
              (,nj (cond ((/= ,nca ,nb) (different-length-warn ,nca ,nb)
                                        (min ,nca ,nb))
                         (t ,nca)))
              (,m (block-size (min ,nra ,nj))))
         (declare (type fixnum ,nra ,nca ,nb ,nj ,m))
         (if (= ,m 0)
             (,calc 0 ,nra 0 ,nj ,nra ,nca ,ma ,vb ,vc)
             (dotimes-interval (,i ,m ,nra)
               (,calc ,i ,m 0 ,nj ,nra ,nca ,ma ,vb ,vc)))))))


(declaim (ftype (function ((simple-array double-float (* *))
                           (simple-array double-float (*))
                           (simple-array double-float (*))))
                dm*v))
(defun dm*v (ma vb vc)
  "Multiply matrix and vector of double-float"
  (declare (type (simple-array double-float (* *)) ma)
           (type (simple-array double-float (*)) vb vc))
  (m*v double-float ma vb vc))


(declaim (ftype (function ((simple-array long-float (* *))
                           (simple-array long-float (*))
                           (simple-array long-float (*))))
                lm*v))
(defun lm*v (ma vb vc)
  "Multiply matrix and vector of long-float"
  (declare (type (simple-array long-float (* *)) ma)
           (type (simple-array long-float (*)) vb vc))
  (m*v long-float ma vb vc))
