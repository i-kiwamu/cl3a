(in-package :cl-user)
(defpackage cl3a.mvmult
  (:use :cl :alexandria :trivial-types)
  (:shadowing-import-from :trivial-types
                          :proper-list
                          :proper-list-p
                          :string-designator)
  (:import-from :cl3a.utilities
                :different-length-warn
                :min-factor
                :dotimes-unroll
                :dmvv-calc-within-L1
                :lmvv-calc-within-L1)
  (:export :dm*v :lm*v))
(in-package :cl3a.mvmult)


(defmacro m*v-ker (val-type si ni sj nj nr nc ma vb)
  "Multiply matrix and vector"
  (with-gensyms (nw nv iend jend jend0 res i isi j j1 j2 j3 j4 maxj)
    `(let* ((,nv (min ,nr ,ni))
            (,nw (min ,nc ,nj))
            (,iend (min ,nr (the fixnum (+ ,si ,ni))))
            (,jend (min ,nc (the fixnum (+ ,sj ,nj))))
            (,jend0 (min-factor ,jend 5))
            (,res (make-array (list ,nv) :element-type ',val-type)))
       (declare (type fixnum ,nv ,nw ,iend ,jend ,jend0)
                (type (simple-array ,val-type (*)) ,res))
       (cond
         ((< ,nw 5)
          (do ((,i ,si (1+ ,i)))
              ((>= ,i ,iend))
            (let ((,isi (- (the fixnum ,i) ,si)))
              (declare (type fixnum ,isi))
              (do ((,j ,sj (1+ ,j)))
                  ((>= ,j ,jend))
                (incf (aref ,res ,isi)
                      (* (aref ,ma ,i ,j) (aref ,vb ,j)))))))
         (t (do ((,i ,si (1+ ,i)))
                ((>= ,i ,iend) ,i)
              (let* ((,isi (- (the fixnum ,i) ,si))
                     (,maxj
                      (do ((,j ,sj (+ ,j 5))
                           (,j1 (the fixnum (+ ,sj 1)) (+ (the fixnum ,j1) 5))
                           (,j2 (the fixnum (+ ,sj 2)) (+ (the fixnum ,j2) 5))
                           (,j3 (the fixnum (+ ,sj 3)) (+ (the fixnum ,j3) 5))
                           (,j4 (the fixnum (+ ,sj 4)) (+ (the fixnum ,j4) 5)))
                          ((>= ,j ,jend0) ,j)
                        (incf (aref ,res ,isi)
                              (+ (* (aref ,ma ,i ,j) (aref ,vb ,j))
                                 (* (aref ,ma ,i ,j1) (aref ,vb ,j1))
                                 (* (aref ,ma ,i ,j2) (aref ,vb ,j2))
                                 (* (aref ,ma ,i ,j3) (aref ,vb ,j3))
                                 (* (aref ,ma ,i ,j4) (aref ,vb ,j4)))))))
                (declare (type fixnum ,isi ,maxj))
                (when (< ,maxj ,jend)
                  (do ((,j ,maxj (1+ ,j)))
                      ((>= ,j ,jend))
                    (incf (aref ,res ,isi)
                          (* (aref ,ma ,i ,j) (aref ,vb ,j)))))))))
       ,res)))


(declaim (inline dm*v-ker)
         (ftype (function (fixnum fixnum fixnum fixnum fixnum fixnum
                           (simple-array double-float (* *))
                           (simple-array double-float (*)))
                          (simple-array double-float (*)))
                dm*v-ker))
(defun dm*v-ker (si ni sj nj nr nc ma vb)
  "Multiply matrix and vector of double-float"
  (declare (optimize (speed 3) (debug 0) (safety 0))
           (type fixnum si ni sj nj nr nc)
           (type (simple-array double-float (* *)) ma)
           (type (simple-array double-float (*)) vb))
  (m*v-ker double-float si ni sj nj nr nc ma vb))


(declaim (inline lm*v-ker)
         (ftype (function (fixnum fixnum fixnum fixnum fixnum fixnum
                           (simple-array long-float (* *))
                           (simple-array long-float (*)))
                          (simple-array long-float (*)))
                lm*v-ker))
(defun lm*v-ker (si ni sj nj nr nc ma vb)
  "Multiply matrix and vector of long-float"
  (declare (optimize (speed 3) (debug 0) (safety 0))
           (type fixnum si ni sj nj nr nc)
           (type (simple-array long-float (* *)) ma)
           (type (simple-array long-float (*)) vb))
  (m*v-ker long-float si ni sj nj nr nc ma vb))


(declaim (ftype (function ((simple-array double-float (* *))
                           (simple-array double-float (*)))
                          (simple-array double-float (*)))
                dm*v))
(defun dm*v (ma vb)
  "Multiply matrix and vector of double-float"
  (declare (optimize (speed 3))
           (type (simple-array double-float (* *)) ma)
           (type (simple-array double-float (*)) vb))
  (let* ((nr (array-dimension ma 0))
         (nc-orig (array-dimension ma 1))
         (nb (length vb))
         (nc (cond ((/= nc-orig nb) (different-length-warn nc-orig nb)
                                    (min nc-orig nb))
                   (t nc-orig)))
         (res (dmvv-calc-within-L1 #'dm*v-ker nr nc ma vb))
         (vc (make-array (list nr) :element-type 'double-float))
         (p 0))
    (declare (type fixnum nr nc p)
             (type (proper-list (simple-array double-float (*))) res)
             (type (simple-array double-float (*)) vc))
    (dolist (r res)
      (declare (type (simple-array double-float (*)) r))
      (let ((nir (length r)))
        (declare (type fixnum nir))
        (setf (subseq vc p (+ p nir)) r)
        (incf p nir)))
    vc))


(declaim (ftype (function ((simple-array long-float (* *))
                           (simple-array long-float (*)))
                          (simple-array long-float (*)))
                lm*v))
(defun lm*v (ma vb)
  "Multiply matrix and vector of long-float"
  (declare (optimize (speed 3))
           (type (simple-array long-float (* *)) ma)
           (type (simple-array long-float (*)) vb))
  (let* ((nr (array-dimension ma 0))
         (nc-orig (array-dimension ma 1))
         (nb (length vb))
         (nc (cond ((/= nc-orig nb) (different-length-warn nc-orig nb)
                                    (min nc-orig nb))
                   (t nc-orig)))
         (res (lmvv-calc-within-L1 #'lm*v-ker nr nc ma vb))
         (vc (make-array (list nr) :element-type 'long-float))
         (p 0))
    (declare (type fixnum nr nc-orig nb nc p)
             (type (proper-list (simple-array long-float (*))) res)
             (type (simple-array long-float (*)) vc))
    (dolist (r res)
      (declare (type (simple-array long-float (*)) r))
      (let ((nir (length r)))
        (declare (type fixnum nir))
        (setf (subseq vc p (+ p nir)) r)
        (incf p nir)))
    vc))
