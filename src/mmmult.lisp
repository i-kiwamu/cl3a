(in-package :cl-user)
(defpackage cl3a.mmmult
  (:use :cl :alexandria :cl3a.utilities)
  (:export :dm*m :lm*m))
(in-package :cl3a.mmmult)


(defmacro m*m-ker (val-type si ni sj nj nra nca ncb ma mb mc)
  "Multiply matrix and matrix"
  (with-gensyms (iend jend i j k maik imb imc)
    `(let* ((,iend (min ,nra (the fixnum (+ ,si ,ni))))
            (,jend (min ,ncb (the fixnum (+ ,sj ,nj)))))
       (declare (type fixnum ,iend ,jend))
       (do ((,i ,si (1+ ,i)))
           ((>= ,i ,iend))
         (do ((,k 0 (1+ ,k)))
             ((>= ,k ,nca))
           (let ((,maik (aref ,ma ,i ,k))
                 (,imb (array-row-major-index ,mb ,k ,sj))
                 (,imc (array-row-major-index ,mc ,i ,sj)))
             (declare (type ,val-type ,maik)
                      (type fixnum ,imb ,imc))
             (do ((,j ,sj (1+ ,j)))
                 ((>= ,j ,jend))
               (incf (row-major-aref ,mc ,imc)
                     (* ,maik (row-major-aref ,mb ,imb)))
               (incf ,imb)
               (incf ,imc))))))))


(defmacro m*m (val-type ma mb mc)
  (with-gensyms (calc nra nca nrb ncb nv nt m i j)
    `(flet ((,calc (si ni sj nj nra nv ncb ma mb mc)
              (declare (optimize (speed 3) (debug 0) (safety 0))
                       (type fixnum si ni sj nj nra nv ncb)
                       (type (simple-array ,val-type (* *)) ma mb mc))
              (m*m-ker ,val-type si ni sj nj nra nv ncb ma mb mc)))
       (declare (inline ,calc))
       (let* ((,nra (array-dimension ,ma 0))
              (,nca (array-dimension ,ma 1))
              (,nrb (array-dimension ,mb 0))
              (,ncb (array-dimension ,mb 1))
              (,nv (cond ((/= ,nca ,nrb)
                          (different-length-warn ,nca ,nrb)
                          (min ,nca ,nrb))
                         (t ,nca)))
              (,nt (min ,nra ,ncb))
              (,m (block-size ,nt)))
         (declare (type fixnum ,nra ,nca ,nrb ,ncb ,nv ,nt ,m))
         (if (= ,m 0)
             (,calc 0 ,nra 0 ,ncb ,nra ,nv ,ncb ,ma ,mb ,mc)
             (dotimes-interval (,i ,m ,nra)
               (dotimes-interval (,j ,m ,ncb)
                 (,calc ,i ,m ,j ,m ,nra ,nv ,ncb ,ma ,mb ,mc))))))))


(declaim (ftype (function ((simple-array double-float (* *))
                           (simple-array double-float (* *))
                           (simple-array double-float (* *))))
                dm*m))
(defun dm*m (ma mb mc)
  "Multiply matrix and matrix of double-float"
  (declare (type (simple-array double-float (* *)) ma mb mc))
  (m*m double-float ma mb mc))


(declaim (ftype (function ((simple-array long-float (* *))
                           (simple-array long-float (* *))
                           (simple-array long-float (* *))))
                lm*m))
(defun lm*m (ma mb mc)
  (declare (type (simple-array long-float (* *)) ma mb mc))
  (m*m long-float ma mb mc))
